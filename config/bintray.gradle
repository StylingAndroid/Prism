apply from: '../config/maven.gradle'
apply plugin: 'com.jfrog.bintray'

version = libraryVersion

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
File localProperties = project.rootProject.file('local.properties');
if (localProperties.exists()) {
    properties.load(localProperties.newDataInputStream())
} else {
    properties.put("bintray.user", project.bintrayUser)
    properties.put("bintray.apikey", project.bintrayApikey)
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    dryRun = bintrayDryRun

    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = libraryName
        desc = libraryDescription
        websiteUrl = 'https://github.com/StylingAndroid/Prism'
        vcsUrl = 'https://github.com/StylingAndroid/Prism.git'
        licenses = ["Apache-2.0"]
        version {
            name = libraryVersion
            desc = libraryDescription
         }
    }
}
